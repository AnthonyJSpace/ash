crackCommand = function(params)
	crypto = include_lib("/lib/crypto.so")
	if not crypto then
		crypto = include_lib(current_path + "/crypto.so")
		if not crypto then
			UI.Error("Unable to load crypto.so")
			return ERROR
		end if
	end if

	user = ""
	hash = params[0]
	if hash.indexOf(":") then
		user = hash.split(":")[0]
		hash = hash.split(":")[1]
	end if

	if hash.len != 32 then
		UI.Error("Invalid hash length")
		return ERROR
	end if

	result = crypto.decipher(hash)
	if result == null then
		UI.Error("Decipher failed")
		return ERROR
	end if

	if user != "" then
		UI.Print(
			color.txt +
				user +
				":" +
				result +
				color.close)
	else
		UI.Print(color.txt + result + color.close)
	end if

	return SUCCESS
end function
commandMap = {
	"command": "crack",
	"params": [],
	"callback": @crackCommand,
	"description": "Crack a password hash",
	"usage": "crack [hash]",
	"minargs": 1,
	"requirement": null,
}
classCommand.New(commandMap)